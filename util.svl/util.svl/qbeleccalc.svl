#svl
//    qbchgcalc.svl    Recursively descends the given directory and 
//                     calculates the "electron count" for PDB, MOE, MOL2,
//                     MDLMOL and MACMOD molecular files.
//
//                     NOTE:  writes results in electons.csv in cwd.
//
//    Command:
//        moebatch -exec "run ['qbeleccalc.svl', ['directory']]" -exit
//

#set title   'QuantumBio Electron Count Calculator'
#set class   'QuantumBio'
#set version '2009.01'

local function calc [dir, outf]
    local filelist = flist [dir];
    local filename;
    for filename in filelist loop
        local chains = [];
        local type = ftype filename;
        if type == 'dir' then
            calc [filename, outf];
        else
            local extension = fext filename;
            if extension === 'pdb' then
	            chains = ReadPDB filename;
            elseif extension === 'moe' then
    	        chains = ReadMOE filename;
            elseif extension === 'mol2' then
    	        chains = ReadTriposMOL2 filename;
            elseif extension === 'mdl_mol' then
    	        chains = ReadMDLMOL filename;
            elseif extension === 'macmod' then
    	        chains = ReadMacroModel filename;
            endif

            if chains === [] then
                continue;
            endif

            local atoms = cat cAtoms chains;
            local numelecs = add aAtomicNumber atoms;

            fwrite [outf, '{c:}, {c:}\n', 
                filename, select ['even', 'odd', even numelecs]];

            oDestroy chains;
        endif
    endloop
endfunction

local function main [dirpath]
    local outfile = fopenw 'electrons.csv';
    fwrite [outfile, 'Molecule, Electrons\n'];
    calc [dirpath, outfile];
    fclose outfile;
endfunction

