#svl
//    qbchgcalc.svl    Recursively descends the given directory and 
//                     calculates the formal and partial charges for 
//                     PDB, MOE, MOL2, MDLMOL and MACMOD molecular files.
//
//                     NOTE:  writes results in charges.csv in cwd.
//
//    Command:
//        moebatch -exec "run ['qbchgcalc.svl', ['directory']]" -exit
//

#set title   'QuantumBio Charge Calculator'
#set class   'QuantumBio'
#set version '2009.01'

function PartialCharge;

local function calc [dir, outf]
    local filelist = flist [dir];
    local filename;
    for filename in filelist loop
        local chains = [];
        local type = ftype filename;
        if type == 'dir' then
            calc [filename, outf];
        else
            local extension = fext filename;
            if extension === 'pdb' then
	            chains = ReadPDB filename;
            elseif extension === 'moe' then
    	        chains = ReadMOE filename;
            elseif extension === 'mol2' then
    	        chains = ReadTriposMOL2 filename;
            elseif extension === 'mdl_mol' then
    	        chains = ReadMDLMOL filename;
            elseif extension === 'macmod' then
    	        chains = ReadMacroModel filename;
            endif

            if chains === [] then
                continue;
            endif

            local atoms = cat cAtoms chains;
            local expformalchg = add aExplicitFCharge atoms;
            local formalchg = add aFCharge atoms;
            local ion = add aIon atoms;
            local storedpartchg = add aCharge atoms;

            local ffpartchg = add first PartialCharge [atoms, 'FF'];
            local amberpartchg = add first PartialCharge [atoms, 'AMBER99'];
            local peoepartchg = add first PartialCharge [atoms, 'PEOE'];
            local mmffpartchg = add first PartialCharge [atoms, 'MMFF94*'];

            local numelecs = add aAtomicNumber atoms;

            fwrite [outf, '{c:}, {c:}, {n:}, {n:}, {n:}, {n:.3f}, {n:.3f}, {n:.3f},'
                ' {n:.3f}, {n:.3f}\n', 
                filename, select ['even', 'odd', even numelecs],
                ion, formalchg, expformalchg, storedpartchg,
                ffpartchg, amberpartchg, peoepartchg, mmffpartchg];

            oDestroy chains;
        endif
    endloop
endfunction

local function main [dirpath]
    local outfile = fopenw 'charges.csv';
    fwrite [outfile, 'Molecule, Electrons, Ion, Formal, Exp Formal, Stored, FF, AMBER99,'
        ' Gasteiger, MMFF94*\n'];
    calc [dirpath, outfile];
    fclose outfile;
endfunction

