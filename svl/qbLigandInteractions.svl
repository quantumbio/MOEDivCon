#svl

function prolig_Calculate;
function _Atoms;

const INTERACTION_TYPES = [
    'hbond',	// hydrogen bond donor (H goes from A --> B)
    'metal',	// metal-ligation
    'ionic',	// electrostatic interaction between (+) and (-)
    'arene',	// pi-pi, pi-cation, pi-H interactions
    'covalent'	// an actual bond between ligand & receptor blocks
    , 'distance'	// interactions by virtue of being close
];

// Collapse atom:multi-atoms interactions in the data vector to the
// atom:atom interaction with the shortest distance.
const COLLAPSE_MULT_ATOM_TO_NEAREST = 1;// how to collapse multi-atom interacts
local function collapse_mult_atom_interactions [a1, a2]

    local function getnearatoms [a, b]
	a = stretch [a, length b];
	b = resize  [b, length a];
	local min = x_min add sqr sub [aPos a, aPos b];
	return [a(min), b(min)];
    endfunction

    if COLLAPSE_MULT_ATOM_TO_NEAREST then
	local am_mask = gtE [app length a1, 1] or gtE [app length a2, 1];
	local am1 = a1 | am_mask;
	local am2 = a2 | am_mask;
	[am1, am2] = tr app getnearatoms tr [am1, am2];
	a1 | am_mask = am1;
	a2 | am_mask = am2;
    else
	a1 = app first a1;
	a2 = app first a2;
    endif
    return [a1, a2];
endfunction

global function qbWriteHeadHTML [fileid,titleTxt]

    fwrite [fileid,'<head><title>{}</title>\n', titleTxt];

    const MYFORMAT =
#string
	<script src="http://downloads.quantumbioinc.com.s3.amazonaws.com/js/jquery-1.10.2.min.js"></script>
	<script src="http://downloads.quantumbioinc.com.s3.amazonaws.com/js/lightbox-2.6.min.js"></script>
	<style>
        .custom_scrollbar {
            margin: 20px auto;
            overflow: scroll;
            padding: 0 20px 0 0;
        }
        .custom_scrollbar::-webkit-scrollbar {
            width: 10px;
            height: 10px;
        }
        .custom_scrollbar::-webkit-scrollbar-track {
            background-color: rgba(113,112,107,0.1);
            -webkit-border-radius: 5px;
        }
        .custom_scrollbar::-webkit-scrollbar-thumb:vertical {
            background-color: rgba(0,0,0,.2);
            -webkit-border-radius: 6px;
        }
        .custom_scrollbar::-webkit-scrollbar-thumb:vertical:hover,
        .custom_scrollbar::-webkit-scrollbar-thumb:horizontal:hover {
            background: gray;
        }
        .custom_scrollbar::-webkit-scrollbar-thumb:horizontal {
            background-color: rgba(0,0,0,.2);
            -webkit-border-radius: 6px;
        }
    </style>
<link rel="stylesheet" type="text/css" href="http://www.quantumbioinc.com/stylesheets/styles.css" />
<link href="http://downloads.quantumbioinc.com.s3.amazonaws.com/css/lightbox.css" rel="stylesheet" />
</head>
<body id='no-sidebar'>
<div id='wrapper'>
<div id='header'>
<div class="header-holder">
  <div class="logo-holder">
    <object classid="clsid:d27cdb6e-ae6d-11cf-96b8-444553540000" codebase="http://fpdownload.macromedia.com/pub/shockwave/cabs/flash/swflash.cab#version=7,0,0,0" width="567" height="111" id="picturechanger" align="middle">
      <param name="allowScriptAccess" value="sameDomain" />
      <param name="movie" value="http://www.quantumbioinc.com/flash/picturechanger.swf" />
      <param name="quality" value="high" />
      <param name="bgcolor" value="#0174DF" />
      <param name="FlashVars" value="xmlURL=http://www.quantumbioinc.com/flash/config.xml" />
<!--      <embed src="http://www.quantumbioinc.com/flash/picturechanger.swf"    -->
      <img src="http://www.quantumbioinc.com/flash/home2.jpg"    
      FlashVars="xmlURL=http://www.quantumbioinc.com/flash/config.xml" 
      quality="high" bgcolor="#0174DF" width="567" height="111"  name="picturechanger" allowScriptAccess="sameDomain" 
      type="application/x-shockwave-flash" pluginspage="http://www.macromedia.com/go/getflashplayer" />
    </object>
    <strong class="logo"><a href="http://www.quantumbioinc.com/" target="QBWEB">QuantumBio</a></strong>
    <strong class="subttl">A Quantum Breakthrough in Drug Discovery</strong>
  </div>
</div>
</div>
<ul id='nav'>
<li>
<a href="http://www.quantumbioinc.com/" target="QBWEB">Home</a>
</li>
<li>
<a href="http://www.quantumbioinc.com/products" target="QBWEB">Products</a>
</li>
<li>
<a href="http://www.quantumbioinc.com/careers" target="QBWEB">Careers</a>
</li>
<li>
<a href="http://www.quantumbioinc.com/news" target="QBWEB">News & Events</a>
</li>
<li class='active'>
<a href="http://www.quantumbioinc.com/support" target="QBWEB">Support</a>
</li>
<li>
<a href="http://www.quantumbioinc.com/about_us" target="QBWEB">About QuantumBio</a>
</li>
<li>
<a href="http://www.quantumbioinc.com/contact_us" target="QBWEB">Contact Us</a>
</li>

</ul>
<div id='main'>
<div id='container'>
<div id='content'>
#;

    fwrite [fileid, '{}', MYFORMAT];
    
endfunction

global function qbWriteTailHTML [fileid]

    local version = getenv 'QBBUILDNUM';
    if isnull version then
        version = 'UNKNOWN';
    endif    
    fwrite [fileid, '<BR><div align="right">Data processed using DivCon Discovery Suite build-{} at {}</div>\n', version , asctime []];

    fwrite [fileid, '<BR><BR><div align="left"><p class="copyright">&copy; {} QuantumBio Inc. All Rights Reserved.</p></div>\n', first time [] ];

    const MYFORMAT =
#string

</div>
</div>
</div>

</body>

#;

    fwrite [fileid, '{}', MYFORMAT ];
endfunction

function qbMeasurePotential;
function DrawLigandInteractions;
function DrawAnnotationLegend;
function fwrite_PNG;

local function fcn_delocalize [fcn, file]

    if anytrue app isnull argument[] then
	exit 'Usage: fcn_delocalize [fcn, file]';
    endif

    if ftype file <> 'file' then
	exit 'Invalid file';
    endif

    if anyfalse app tok_length fcn then
	exit 'Invalid fcn';
    endif

    local fcnquery, fnum, fout, foutname, line, seen = 0;

    fcnquery = apt twrite ['local *function *{}*', fcn];

    fnum = fopenr file;
    fout = fopentemp '$TMP/fcn_delocalize*.svl';
    foutname = fname fout;

    while length ([line] = freadb [fnum, 'line', 1]) loop
	if length findmatch [fcnquery, token line] then
	    line = cat ["global ", drop [line, dec strpos ["function", line]]];
	    seen =  1;
	endif
	fwrite [fout, '{}\n', line];
    endloop
    fflush fout;

    if istrue seen then
	unload file;
	load fname fout;
    endif

    fclose fnum;
    fclose fout;
endfunction

// qbmoebatch -exec -licwait "qbLigandInteractions ['1HWI.pdb','1HWI_refine_001.pdb','115']"
 
global function qbLigandInteractions [firstFile, finalFile, interestLigand]

    oDestroy Chains[];

    local firstChains = ReadAuto firstFile;
    local firstLigands = _Atoms '$$ligand' | m_join [aChain _Atoms '$$ligand', firstChains];
    firstLigands = uniq aResidue firstLigands;

    local finalChains = ReadAuto finalFile;
    local finalLigands = _Atoms '$$ligand' | m_join [aChain _Atoms '$$ligand', finalChains];
    finalLigands = uniq aResidue finalLigands;
    
    if not isnull interestLigand then
        firstLigands = firstLigands | rName firstLigands == interestLigand;
        finalLigands = finalLigands | rName finalLigands == interestLigand;
    endif
    
    if length cat rAtoms firstLigands <> length cat rAtoms finalLigands then
        write ['ERROR: the number of ligand atoms differ between {} and {}\n', firstFile, finalFile];
        return [];
    endif
    
    if isnull Chains [] then
        write ['ERROR: Files not found or some sort of parsing error\n'];
        return [];
    endif
    
    local outfileBase = twrite ['{}-{}', fbase ftail firstFile, fbase ftail finalFile];
    local htmlFile = fopenw twrite ['{}.html', outfileBase];
    local csvFile = fopenw twrite ['{}.csv', outfileBase];
    
    qbWriteHeadHTML [htmlFile,outfileBase];
    
    fwrite [htmlFile, '<H1>Active Site Analysis</H1>\n'];
    fwrite [htmlFile, '<H3>Initial = {}</H3>\n', firstFile];
    fwrite [htmlFile, '<H3>Final = {}</H3>\n', finalFile];
    
    if isnull interestLigand then
        fwrite [htmlFile, '<H3>Ligand = {}</H3>\n', 'all'];
    else
        fwrite [htmlFile, '<H3>Ligand = {}</H3>\n', interestLigand];
    endif
    
    fwrite [htmlFile, '<BR><HR width=80%><BR>\n'];
    
    fwrite [htmlFile, '<div class="custom_scrollbar">\n<TABLE cellpadding="5" style="color:#000000;  font-size:13px">\n'];
    local tmpInteraction;
    // spreadsheet heading
    fwrite [htmlFile, '<TR><TH>{}</TH><TH>{}</TH><TH>{}</TH><TH>{}</TH><TH colspan=2>{}',
        ' ',
        ' ',
        ' ',
//        ' ',
        ' ',
        'InteractionE'
    ];
    fwrite [csvFile,'{},{},{},{},{},{},{}',
        ' ',
        ' ',
        ' ',
        ' ',
        ' ',
        'IntE',
        ' '
    ];
    fwrite [htmlFile, '</TH><TH colspan=2>Analysis Maps\n'];
    fwrite [csvFile, ',{},{}',
        'Analysis Maps',
        ''
    ];
    for tmpInteraction in INTERACTION_TYPES loop
        fwrite [htmlFile, '</TH><TH colspan=4>{}',
            tmpInteraction
        ];
        fwrite [csvFile,',{},{},{},{}',
            tmpInteraction,
            ' ',
            ' ',
            ' '
        ];
    endloop
    fwrite [htmlFile, '<TR><TH>{}</TH><TH>{}</TH><TH>{}</TH><TH>{}</TH><TH>{}</TH><TH>{}',
        'PDBid',
        'Ligand',
        'ID',
        'Chain',
//        'atmCount',
        'Initial',
        'Final'
    ];
    fwrite [csvFile,'\n'];
    fwrite [csvFile,'{},{},{},{},{},{},{}',
        'PDBid',
        'Ligand',
        'ID',
        'Chain',
        'atmCount',
        'Initial',
        'Final'
    ];
    fwrite [htmlFile, '</TH><TH style="border: solid 0 #0000FF; border-left-width:2px; padding-left:0.5ex">{}</TH><TH>{}',
        'Initial',
        'Final'
    ];
    fwrite [csvFile,',{},{}', 
        'Initial',
        'Final'
    ];
    for tmpInteraction in INTERACTION_TYPES loop
        fwrite [htmlFile, '</TH><TH style="border: solid 0 #0000FF; border-left-width:2px; padding-left:0.5ex">{}</TH><TH>{}</TH><TH>{}</TH><TH>{}',
            'I',
            'F',
            'F+',
            'F-'
        ];
        fwrite [csvFile,',{},{},{},{}',
            'Initial',
            'Final',
            'False+',
            'False-'
        ];
    endloop
    fwrite [htmlFile, '</TH></TR>\n'];
    fwrite [csvFile, '\n'];
    
    // get the ligand interaction legend
    fcn_delocalize ['DrawAnnotationLegend', '$MOE/lib/svl/run/prolig2d.svl'];
    local grleg = DrawAnnotationLegend[];
    unload sym_file 'DrawAnnotationLegend'; 

    local ligandCount;
    for ligandCount = 1, length firstLigands, 1 loop

        local firstLigandAtoms = cat rAtoms firstLigands(ligandCount);
        local firstReceptor = diff [cat cAtoms firstChains, firstLigandAtoms];
        local firstPotData = qbMeasurePotential[firstLigandAtoms,firstReceptor];
        local firstInteractions = prolig_Calculate [ INTERACTION_TYPES, firstLigandAtoms, firstReceptor];
        local [int2,int3] = collapse_mult_atom_interactions[firstInteractions(2),firstInteractions(3)];
        firstInteractions(2) = int2;
        firstInteractions(3) = int3;

        local finalLigandAtoms = cat rAtoms finalLigands(ligandCount);
        local finalReceptor = diff [cat cAtoms finalChains, finalLigandAtoms];
        local finalPotData = qbMeasurePotential[finalLigandAtoms,finalReceptor];
        local finalInteractions = prolig_Calculate [ INTERACTION_TYPES, finalLigandAtoms, finalReceptor];
        [int2,int3] = collapse_mult_atom_interactions[finalInteractions(2),finalInteractions(3)];
        finalInteractions(2) = int2;
        finalInteractions(3) = int3;

        local PDBid;
        PDBid = ' ';
        if ligandCount == 1 then
            PDBid = fbase first cTag firstChains;
        endif
        
        //      cTag, ligName, ligUID, ligChain, atmCount, firstIntE, finalIntE, firstLiand, finalLigand, falsePositives, falseNegatives
        fwrite [htmlFile, '<TR><TD>{}</TD><TD>{}</TD><TD>{}</TD><TD>{}</TD><TD>{}</TD><TD>{}',
            PDBid,
            rName firstLigands(ligandCount),
            rUID firstLigands(ligandCount),
            fext cName rChain firstLigands(ligandCount),
//            length cat rAtoms firstLigands(ligandCount),
            add second untag third firstPotData,
            add second untag third finalPotData
        ];
        fwrite [csvFile,'{},{},{},{},{},{},{}',
            PDBid,
            rName firstLigands(ligandCount),
            rUID firstLigands(ligandCount),
            fext cName rChain firstLigands(ligandCount),
            length cat rAtoms firstLigands(ligandCount),
            add second untag third firstPotData,
            add second untag third finalPotData
        ];        

    // write out the png file associated with the interaction diagram
        local twoDDiagrams = DrawLigandInteractions [[firstLigandAtoms,finalLigandAtoms],[firstReceptor,finalReceptor], 
            [simultaneous:1,showconserved:1,rescontacts:1,arenecontacts:1,implicit_hydrogens:0,size:[1000,1000]]];
        local baseName = twrite ['{}_{}_{}',
            rName firstLigands(ligandCount),
            rUID firstLigands(ligandCount),
            fext cName rChain firstLigands(ligandCount)
        ];
        
        local initialPNG = twrite ['I-{}.png',baseName];
        local finalPNG = twrite ['F-{}.png',baseName];
        
        fwrite [htmlFile, '</TD><TD style="border: solid 0 #0000FF; border-left-width:2px; padding-left:0.5ex"><nobr><a href="{}" data-lightbox="{}" title="Initial Refinement: {} (click for next)">{}</a></nobr></TD>',
            initialPNG,baseName,initialPNG,initialPNG];
        fwrite [htmlFile, '<TD><nobr><a href="{}" data-lightbox="{}" title="Final Refinement: {} (click for previous)">{}</a></nobr>',
            finalPNG,baseName,finalPNG,finalPNG];
        
        fwrite [csvFile, ',{},{}', initialPNG, finalPNG];

        local size = cat [
            (gr_header twoDDiagrams(1)).size,
            (gr_header grleg).size
        ];
        size = [max size[[1,3]], add size[[2,4]]];  // [width, height]
        const padding = 10;
        size = size + (2 * padding);
        local pminit = rep [rep [0x00FFFFFF, size(2)], size(1)];
        pminit = gr_render_pixmap [pminit, grleg,  [first size - padding - first (gr_header grleg).size,padding,1,1] ];

        twoDDiagrams(1) = gr_render_pixmap [pminit, twoDDiagrams(1),  [padding, second (gr_header grleg).size + padding,1,1] ];
        twoDDiagrams(2) = gr_render_pixmap [pminit, twoDDiagrams(2),  [padding, second (gr_header grleg).size + padding,1,1] ];
  
        fwrite_PNG [ initialPNG, twoDDiagrams(1)];
        fwrite_PNG [ finalPNG, twoDDiagrams(2)];
        
        for tmpInteraction in INTERACTION_TYPES loop

            local firstLigandMask = 
                ((m_join [aResidue firstInteractions(2),firstLigands(ligandCount)]) 
                    or (m_join [aResidue firstInteractions(3),firstLigands(ligandCount)]));
            local firstIntMask = (m_join [first firstInteractions, tmpInteraction]);
            local finalLigandMask = 
                ((m_join [aResidue finalInteractions(2),finalLigands(ligandCount)])
                    or (m_join [aResidue finalInteractions(3),finalLigands(ligandCount)]));
            local finalIntMask = (m_join [first finalInteractions, tmpInteraction]);

            firstLigandMask = firstLigandMask and firstIntMask;
            finalLigandMask = finalLigandMask and finalIntMask;
            local firstIntTextLig = apt twrite ['{}:{}:{}-{}:{}:{}\n',
                rName aResidue (firstInteractions(2)) | firstLigandMask, 
                rUID aResidue (firstInteractions(2)) | firstLigandMask, 
                aName (firstInteractions(2)) | firstLigandMask,
                rName aResidue (firstInteractions(3)) | firstLigandMask, 
                rUID aResidue (firstInteractions(3)) | firstLigandMask, 
                aName (firstInteractions(3)) | firstLigandMask
            ];
            local finalIntTextLig = apt twrite ['{}:{}:{}-{}:{}:{}\n',
                rName aResidue (finalInteractions(2)) | finalLigandMask, 
                rUID aResidue (finalInteractions(2)) | finalLigandMask, 
                aName (finalInteractions(2)) | finalLigandMask,
                rName aResidue (finalInteractions(3)) | finalLigandMask, 
                rUID aResidue (finalInteractions(3)) | finalLigandMask, 
                aName (finalInteractions(3)) | finalLigandMask
            ];

            firstLigandMask = firstIntMask;
            finalLigandMask = finalIntMask;            
            local firstIntTextAll = apt twrite ['{}:{}:{}-{}:{}:{}\n',
                rName aResidue (firstInteractions(2)) | firstLigandMask, 
                rUID aResidue (firstInteractions(2)) | firstLigandMask, 
                aName (firstInteractions(2)) | firstLigandMask,
                rName aResidue (firstInteractions(3)) | firstLigandMask, 
                rUID aResidue (firstInteractions(3)) | firstLigandMask, 
                aName (firstInteractions(3)) | firstLigandMask
            ];
            local finalIntTextAll = apt twrite ['{}:{}:{}-{}:{}:{}\n',
                rName aResidue (finalInteractions(2)) | finalLigandMask, 
                rUID aResidue (finalInteractions(2)) | finalLigandMask, 
                aName (finalInteractions(2)) | finalLigandMask,
                rName aResidue (finalInteractions(3)) | finalLigandMask, 
                rUID aResidue (finalInteractions(3)) | finalLigandMask, 
                aName (finalInteractions(3)) | finalLigandMask
            ];

            local tmpInitialIntLig, tmpInitialIntNet, tmpFinalIntLig, tmpFinalIntNet, 
                    tmpFalsePosLig, tmpFalsePosNet, tmpFalseNegLig, tmpFalseNegNet;
                    
            tmpInitialIntLig = '-';
            tmpInitialIntNet = '-';
            tmpFinalIntLig = '-';
            tmpFinalIntNet = '-';
            tmpFalsePosLig = '-';
            tmpFalsePosNet = '-';
            tmpFalseNegLig = '-';
            tmpFalseNegNet = '-';
            
            if length firstIntTextAll or length finalIntTextAll then
                tmpInitialIntLig = length firstIntTextLig;
                tmpInitialIntNet = twrite ['({})',length firstIntTextAll];
                tmpFinalIntLig = length finalIntTextLig;
                tmpFinalIntNet = twrite ['({})',length finalIntTextAll];
                tmpFalsePosLig = length firstIntTextLig - length pack indexof [firstIntTextLig, finalIntTextLig];
                tmpFalsePosNet = twrite ['({})',length firstIntTextAll - length pack indexof [firstIntTextAll, finalIntTextAll]];
                tmpFalseNegLig = length finalIntTextLig - length pack indexof [finalIntTextLig, firstIntTextLig];
                tmpFalseNegNet = twrite ['({})',length finalIntTextAll - length pack indexof [finalIntTextAll, firstIntTextAll]];
            endif

            fwrite [htmlFile, '</TD><TD style="border: solid 0 #0000FF; border-left-width:2px; padding-left:0.5ex"><nobr>{}{}</nobr></TD><TD><nobr>{}{}</nobr></TD><TD><nobr>{}{}</nobr></TD><TD><nobr>{}{}</nobr>',
                tmpInitialIntLig,
                tmpInitialIntNet,
                tmpFinalIntLig,
                tmpFinalIntNet,
                tmpFalsePosLig,
                tmpFalsePosNet,
                tmpFalseNegLig,
                tmpFalseNegNet
            ];
            fwrite [csvFile,',{}{},{}{},{}{},{}{}',
                tmpInitialIntLig,
                tmpInitialIntNet,
                tmpFinalIntLig,
                tmpFinalIntNet,
                tmpFalsePosLig,
                tmpFalsePosNet,
                tmpFalseNegLig,
                tmpFalseNegNet
            ];

        endloop
        
        fwrite [htmlFile, '</TD></TR>\n'];
        fwrite [csvFile, '\n'];

    endloop

    fwrite [htmlFile, '</TABLE></div>\n'];

    fwrite [htmlFile, '<div align="right">See: <a href="http://www.quantumbioinc.com/products/phenix_divcon" target="QBWEB">http://www.quantumbioinc.com/products/phenix_divcon</a></div>\n'];
    const MYFORMAT =
#string
<BR><BR>
<div style="border:1px solid blue;padding:10px;width:80%">
<p>
<h2>Legend</h2>
<ul>
<li>Interaction Energy = MOE Amber12EHT (in kcal/mol)</li>
<li>Value(Value) = count of interactions between target:ligand (count of interactions within immediate network)</li>
<li>F+ = False+ = interactions observed in the Initial model that are removed in the Final model</li>
<li>F- = False- = interactions rescued in the Final model but not captured in the Initial model</li>
<li>F = Final or the interaction count associated with the final model.</li>
<li>I = Initial or the interaction count associated with the initial model.</li>
<li>Distance = counts of atom-atom distances less then or equal to 4.5 &#8491;.</li>
</ul>
</p>
</div>
#;

    fwrite [htmlFile, '{}', MYFORMAT ];

    qbWriteTailHTML [htmlFile];
    
    fclose htmlFile;
    fclose csvFile;

endfunction

global function qbAnalSelectedInteractions []
    local tmpAtomA, tmpAtomB;
    local selAtoms = Atoms [] | aSelected Atoms[];
    for tmpAtomA in selAtoms loop
        for tmpAtomB in selAtoms loop
            local finalPotData = qbMeasurePotential[tmpAtomA,tmpAtomB];
            write ['{}-{} {}\n', aName tmpAtomA, aName tmpAtomB, finalPotData];
//            exit[];
        endloop
    endloop
endfunction

