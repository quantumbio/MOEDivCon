#svl

//    qmsutil.svl    Utilities to support QMScore data management

#set title   'QMScore Utilities'
#set class   'QM'
#set version '2009.01'

//  Data fields are in the order in which they are returned by Divcon.

const DATA_TAGS =
    [
    "LIGAND",
    "TOTAL SCORE",
    "GAS PHASE HEAT OF FORMATION",
    "ELECTROSTATIC SOLVATION",
    "ATTRACTIVE LENNARD JONES",
    "SOLVATION ENTROPY",
    "VIBRATIONAL ENTROPY",
    "ELECTRONIC INTERACTION ENERGY",
    "MRM STERIC ENERGY",
    "MRM ELECTROSTATIC ENERGY",
    "MRM SOLVATION ENERGY",
    "MRM TOTAL ENERGY"
    ];
 
const DB_FIELDS =
    [
    'Ligand/Pose',
    'QMScore',
    'dHf_g',
    'Solv_elec',
    'LJ_att',
    'S_solv',
    'S_vib',
    'E_eInt',
    'MRMScore',
    'E_stericMRM',
    'E_elecMRM',
    'E_solvMRM'
    ];

// Environment fields
const LIGAND = 'ligand';
const TARGET = 'target';
const OPTIONS = 'options';


/****************************************************************************
 *                          qms_CopyPosesDB
 *
 *  Copy the Poses database into the QMScore output database.
 *
 ****************************************************************************/
global function qms_CopyPosesDB [resultskey, poseskey]

    local [fields, ftypes] = db_Fields poseskey;
    local molfield = fields | (ftypes == 'molecule');

    if 1 <> length molfield then return 0; endif

    //  HACK alert:  temporarily renames mol field to make copying easier
    db_RenameField [resultskey, DB_FIELDS(1), molfield];
    apt db_EnsureField [resultskey,  fields, ftypes];

    local rowkey = 0;

    while rowkey = db_NextEntry [poseskey, rowkey] loop
        local rowdata = db_Read [poseskey, rowkey];
        db_Write [resultskey, 0, rowdata];
    endloop

    db_RenameField [resultskey, molfield, DB_FIELDS(1)];

    return 1;
endfunction

/****************************************************************************
 *                          qms_GetMolField
 *
 ****************************************************************************/
global function qms_GetMolField [dbkey]

    return DB_FIELDS(1);

endfunction

/****************************************************************************
 *                          qms_GetEnv
 *
 *  Get the molecules (ligand and receptor) and options from the QMScore 
 *  database.
 *
 ****************************************************************************/
global function qms_GetEnv [dbkey]
    local tgttok = db_GetEnv [dbkey, TARGET];
    local [[tgtmols]] = sread [string tgttok, '{v}'];
    local ligtok = db_GetEnv [dbkey, LIGAND];
    local [[ligmol]] = sread [string ligtok, '{v}'];
    local opttok = db_GetEnv [dbkey, OPTIONS];
    local [[options]] = sread [string opttok, '{v}'];

    return [tgtmols, ligmol, options];

endfunction

/****************************************************************************
 *                          qms_SaveDBRow
 *
 *  Save a row of data in the QMScore database.
 *
 ****************************************************************************/
global function qms_SaveDBRow [mdbkey, rowkey, ligand, scoredata]

    if alltrue (DATA_TAGS === app first scoredata) then
        local rowdata = tag [DB_FIELDS(1), [ligand]];
        rowdata = cat [rowdata, tag [dropfirst DB_FIELDS, 
            dropfirst app second scoredata]];
        db_Write [mdbkey, rowkey, rowdata];
    endif

endfunction

/****************************************************************************
 *                          qms_SaveScore
 *
 *  Save just the score data in the QMScore database.
 *
 ****************************************************************************/
global function qms_SaveScore [mdbkey, rowkey, scoredata]

    if alltrue (DATA_TAGS === app first scoredata) then
        db_Write [mdbkey, rowkey, tag [dropfirst DB_FIELDS, 
            dropfirst app second scoredata]];
    endif

endfunction

/****************************************************************************
 *                          qms_SaveEnv
 *
 *  Save the molecules (ligand and receptor) and options in the QMScore
 *  database.
 *
 ****************************************************************************/
global function qms_SaveEnv [dbkey, tgtmols, ligmol, options]

    db_SetEnv [dbkey, TARGET, twrite ['{v}', tgtmols]];
    db_SetEnv [dbkey, LIGAND, twrite ['{v}', ligmol]];
    db_SetEnv [dbkey, OPTIONS, twrite ['{v}', options]];

endfunction

/****************************************************************************
 *                          qms_SetFieldNames
 *
 *  Ensure that the required fields are present.
 *
 ****************************************************************************/
global function qms_SetFieldNames [dbkey]

    db_EnsureField [dbkey, DB_FIELDS(1), 'molecule'];
    apt db_EnsureField [dbkey, dropfirst DB_FIELDS, 'double'];

endfunction

