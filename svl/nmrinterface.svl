#svl
#if 0
 // BEGIN COPYRIGHT
 /***********************************************************************
    Copyright (c) 1998-2010 QuantumBio Inc. and/or its affiliates.
	
 This source code is the property of QuantumBio Inc. and/or its affiliates
 and is considered secret.

 This source code contains proprietary and Confidential Information, 
 including trade secrets, belonging to QuantumBio Inc. and/or its 
 affiliates.

 Use, duplication, or disclosure of this source code is strictly 
 prohibited without prior written authorization by the management of 
 QuantumBio Inc. At no time are you authorized to repurpose the source
 code for use with software other then that which it was intended.

 Please see http://www.quantumbioinc.com/ for more information.

 ***********************************************************************/
 // END COPYRIGHT
#endif      

#set title   'QuantumBio NMRScore Interface'
#set class   'QuantumBio'
#set version '2010.01'
#set main 'qb_NMRScore'

function qb_CheckChgTestDivconMols;
function qb_GetElemIndices;
function qb_GetWhich;
function qb_HamSetsInit;
function qb_PassBackbone;

/*  NOTE:  If you change the key lists here, you need to update the
 *         option lists and default indices in qbcore.svl and nmrpanel.svl.
           The unused Hamilitonians were masked when the core was created.
 */

const HAM_KEYS =
    [
    'am1_nmr',
    'mndo_nmr'
    ];

const GEOM_KEYS =
    [
    'NOOPT',
    'HONLYOPT',
    'HETEROOPT',
    'ALLOPT'
    ];

/*  NOTE:  If you change the Element Types here, you need to update them
 *         in nmrpanel.svl.
 */

const ELEM_TYPES =
    [
    'H',
    'C',
    'O',
    'N',
    'F'
    ];


global function qb_NMRScore [recmol, ligmol, coreopts, appopts,
        indices, free, bound]

    local osys, nsys, errmess = "";
    if not MOE_BATCH then
        [osys, nsys] = SystemPush [];
    endif

    //  If there are any errors, return immediately.  Run all error tests.
    qb_HamSetsInit [];
    local [recchains, recok, recchgok, ligchain, ligok, ligchgok, ckmsg]
        = qb_CheckChgTestDivconMols [recmol, coreopts.recchg, ligmol,
            coreopts.ligchg, 1, 1, 1, coreopts.testH, coreopts.testMM,
            'nmr', HAM_KEYS (coreopts.ham)];

    if not alltrue [recok, recchgok, ligok, ligchgok] then
        if not alltrue [recok, ligok] then
            if tok_length ckmsg > 0 then
                errmess = cat [errmess, swrite ['Skipping!\n{}Skipping!',
                    ckmsg]];
            else
                errmess = cat [errmess, 
                    swrite ['Problematic molecule in {}, skipping!\n',
                    qb_GetWhich [not recok, not ligok]]];
            endif
        endif

        if not alltrue [recchgok, ligchgok] then
            errmess =  cat [errmess, 
                swrite ['Problematic charge for {}, skipping!', 
                qb_GetWhich [not recchgok, not ligchgok]]];
        endif

        oDestroy recchains;
        oDestroy ligchain;
        if not MOE_BATCH then
            SystemPop [osys, nsys];
        endif

        return [[], errmess];
    endif

    //  If there are any warnings, pass them up.
    if tok_length ckmsg > 0 then
        errmess = string ckmsg;
    endif

    local subdirname, delsubdir = 0;
    if coreopts.task === [] or coreopts.task === 0 then
        subdirname = coreopts.workdir;
    else
        delsubdir = 1;
        subdirname = twrite ['{}/qb_out{n:}', coreopts.workdir, coreopts.task];
        local okay = _fmkdir subdirname;
        if okay === -1 then
            errmess = cat [swrite ['Unable to create subdirectory:  {}',
                subdirname], errmess];

            oDestroy recchains;
            oDestroy ligchain;
            if not MOE_BATCH then
                SystemPop [osys, nsys];
            endif

            return [[], errmess];
        endif
    endif

    local elems = ELEM_TYPES | [appopts.elemH, appopts.elemC, appopts.elemO, 
            appopts.elemN, appopts.elemF];
    local poseidxs = qb_GetElemIndices [ligmol, elems];

    if not alltrue eqE [indices, poseidxs] then
        errmess = cat [swrite ['Skipping!  Atom indices are inconsistent:\n'
            '    Orig: {v}\n    Pose: {v}\n', indices, poseidxs], errmess];

        oDestroy recchains;
        oDestroy ligchain;
        if not MOE_BATCH then
            SystemPop [osys, nsys];
        endif

        return [[], errmess];
    endif
 
    //  exe_open child process inherits parent's environment.
    local process_key = exe_open ['moedivconnmr', subdirname, [], 1];
    local process_keys = [process_key];
    local status = exe_status process_keys;

    local prios = exe_getpriority process_keys;
    exe_setpriority [process_keys, prios];

    local stdin = exe_stdin process_keys;
    local stdout = exe_stdout process_keys;
    local stderr = exe_stderr process_keys;

    //  Pass the options and the two backbones to QuantumBio's Divcon NMRScore
    //
    //  SplitReceptor ensures that there is only one chain per molecule
    //  in the receptor collection.

    local idx, success = 0;

    fwrite [stdin, '{c:}\n', HAM_KEYS(coreopts.ham)];
    fwrite [stdin, '{c:}\n', select ['NOSTANDARD', 'STANDARD',
        coreopts.scaling === 1]];
    fwrite [stdin, '{c:}\n', GEOM_KEYS(appopts.opt)];
    fwrite [stdin, '{c:}\n', select ['GUESS', 'NOGUESS', appopts.guess]];
    fwrite [stdin, '{n:}\n', length indices];
    for idx = 1, length indices, 1 loop
        fwrite [stdin, '{n:} {n:} {n:}\n', indices (idx), free (idx),
            bound (idx)];
    endloop

    fwrite [stdin, '{n:}\n', coreopts.recchg];
    fwrite [stdin, '{n:}\n', coreopts.ligchg];

    qb_PassBackbone [stdout, stdin, recchains, ligchain];

    oDestroy recchains;
    oDestroy ligchain;
    if not MOE_BATCH then
        SystemPop [osys, nsys];
    endif

    //    results has the format:  [nmrscore, [V1, V2, ... , Vn]]
    local results = [], return_statement;
    local sleep_time = -1;
    local divconerrmess = "";
    while exe_status process_key loop
        if not MOE_BATCH then
            sleep sleep_time;
        endif

        [[return_statement]] = fread [stdout, '{c:*}'];

write ['Divcon: {}\n', return_statement];//DEBUG

        if return_statement === "updatescore" then
            local atomvalues = [];
            local [[score]] = fread [stdout, '{n:}'];
            local [[numvals]] = fread [stdout, '{n:}'];
            for numvals loop
                local [[cspval]] = fread [stdout, '{n:}'];
print [cspval];	//DEBUG
                atomvalues = append [atomvalues, cspval];
            endloop

            results = [score, atomvalues];
            success = 1;
            fwrite [stdin, '{c:}\n', 'Ok'];

        elseif return_statement === "QBException" then
            [[return_statement]] = fread [stdout, '{c:*}'];
            if neL [return_statement, "Divcon done."] then
                divconerrmess = cat [divconerrmess, return_statement];
                [[return_statement]] = fread [stdout, '{c:*}'];
            endif
            while neL [return_statement, "Divcon done."] loop
                divconerrmess = cat [divconerrmess, "\n", return_statement];
                [[return_statement]] = fread [stdout, '{c:*}'];
            endloop;
            fwrite [stdin, '{c:}\n', 'Ok'];
        endif
    endloop

    //  Look for error messages from stderr.
    local [[newerror]] = fread [stderr, '{c:*}'];
    while length newerror > 0 loop
write ['MOEDivcon: {}\n', newerror];//DEBUG
        errmess = cat [newerror, errmess];
        [[newerror]] = fread [stderr, '{c:*}'];
    endloop

    exe_kill  [process_key, 0];
    exe_kill  process_key;
    exe_close process_keys;

    //  Clean up working directory.
    if success === 1 then
        local templist = flist [subdirname, 'MOE_*.dmx*'];
        templist = cat [templist, flist [subdirname, 'MOE_*.row*']];
        templist = cat [templist, flist [subdirname, 'MOE_*.rst*']];
        templist = cat [templist, flist [subdirname, 'MOE_*.out']];
        templist = cat [templist, flist [subdirname, 'divcon.log']];
        templist = cat [templist, flist [subdirname, 'mtkpp.log']];
        templist = cat [templist, flist [subdirname, 'divcon.hss']];
        templist = cat [templist, flist [subdirname, 'fort.8']];
        if delsubdir then
            templist = cat [templist, subdirname];
        endif

        local delerr = app _fdelete templist;
        if anytrue delerr then
            local errfiles = templist | neE [delerr, 0];
            errmess = cat [swrite ['Unable to delete files: {v}',
                errfiles], errmess];
        endif
    endif

    if success === 1 then
        return [results, errmess];
    else
        if isnull divconerrmess then divconerrmess = "Unknown error!"; endif
        if isnull errmess then
            errmess = cat [divconerrmess,
                swrite ['\nLog file:  {}:{}/divcon.log',
                getenv 'HOSTNAME', subdirname]];
        else
            errmess = cat [divconerrmess,
                swrite ['\nLog file:  {}:{}/divcon.log\n',
                getenv 'HOSTNAME', subdirname], errmess];
        endif
        return [[], errmess];
    endif
endfunction

