#svl
#if 0
 // BEGIN COPYRIGHT
 /***********************************************************************
    Copyright (c) 1998-2010 QuantumBio Inc. and/or its affiliates.

 This source code is the property of QuantumBio Inc. and/or its affiliates
 and is considered secret.

 This source code contains proprietary and Confidential Information,
 including trade secrets, belonging to QuantumBio Inc. and/or its
 affiliates.

 Use, duplication, or disclosure of this source code is strictly
 prohibited without prior written authorization by the management of
 QuantumBio Inc. At no time are you authorized to repurpose the source
 code for use with software other then that which it was intended.

 Please see http://www.quantumbioinc.com/ for more information.

 ***********************************************************************/
 // END COPYRIGHT
#endif

//    qmsbatch.svl    Executes QMScore calculations in batch mode

#set title   'QuantumBio QMScore Batch'
#set class   'QuantumBio'
#set version '2010.02'
#set main    'qb_QMScoreBatch'

function qb_GetPoseFields;
function qb_GetRecFields;
function qb_GetEnv;
function qb_SaveDivconStatus;
function qb_SaveQMScore;
function qb_QMScore;

global function qb_QMScoreBatch [dbpath]

    if not MOE_BATCH then return; endif

    //  Extract molecules and run options from the database environment
    local dbkey = db_Open [dbpath, 'read-write'];
    local [recmols, ligmol, s, coreopts, appopts, c] = qb_GetEnv [dbkey];
    local recmolsoktok = db_GetFieldEnv [dbkey, REC_FIELD, RECOK_FLDENV];
    local [[recmolsok]] = sread [string recmolsoktok, '{v}'];
    local ligmolsoktok = db_GetFieldEnv [dbkey, POSE_FIELD, POSEOK_FLDENV];
    local [[ligmolsok]] = sread [string ligmolsoktok, '{v}'];

    local recchg;
    local workdir;
    if coreopts.pwd === 1 then
        workdir = getenv 'PWD';
    else
        workdir = fabsname coreopts.workdir;
    endif

    coreopts.workdir = workdir;
    write ['Work Dir:  {}\n', workdir];  //DEBUG

    //  For each ligand pose in the database, calculate QM score.
    local rowkey = 0, count = 0;
    local total = db_nEntries (dbkey);
    local poseflds = qb_GetPoseFields [dbkey];
    local recflds = qb_GetRecFields [dbkey];
    local tid = task_key -1;
    loop
        if count === 0 or rowkey <> 0 then
            rowkey = db_NextEntry [dbkey, rowkey];
        endif

        if rowkey <> 0 then
            count = count + 1;
            if not recmolsok(count) or not ligmolsok (count) then
                qb_SaveDivconStatus [dbkey, rowkey, 
                    "Error:  Bad molecule, skipping!"];
                continue;
            endif

            coreopts.task = count;
            local [posemol, posechg] = db_ReadFields [dbkey, rowkey, poseflds];

            if coreopts.mode === 'Many-to-Many' then
                [recmols, recchg] = db_ReadFields [dbkey, rowkey, recflds];
            else
                recchg = coreopts.recchg;
            endif
        endif

        coreopts.ligchg = posechg;
        coreopts.recchg = recchg;

        local [[results, errmess], code, seqno, indkey] = mpu_batch [
            select ['', 'call', rowkey === 0],
            'qb_QMScore', [recmols, posemol, coreopts, appopts], [rowkey, tid]];

        if rowkey <> 0 then
            write ['Scoring pose {} of {}\n', count, total];
        endif

        if code === '' then
            if neL [tid, indkey(2)] then
                exit 'MPU/task conflict, aborting calculation.';
            endif

            if results === [] then
                if errmess === [] or errmess === '' then
                    errmess = "Unknown error!";
                endif
                qb_SaveDivconStatus [dbkey, indkey(1),
                  swrite ['Error:  {}', errmess]];
                write ['QMScore Error on pose {}:\n    {}\n', seqno, errmess];
            else
                qb_SaveQMScore [dbkey, indkey(1), results];
                errmess = cat [ "Success\n", errmess];
                qb_SaveDivconStatus [dbkey, indkey(1), errmess];
            endif

        elseif code === 'error' then
            if errmess === [] or errmess === '' then
                errmess = "Unknown error!";
            endif
            qb_SaveDivconStatus [dbkey, indkey(1),
                swrite ['Error:  {}', errmess]];

            write ['QMScore Error on pose {}:\n    {}\n', seqno, errmess];
        endif

    until code === 'eof'
    endloop

    db_Close dbkey;

endfunction
