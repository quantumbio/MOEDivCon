#svl
#if 0
 // BEGIN COPYRIGHT
 /***********************************************************************
    Copyright (c) 1998-2012 QuantumBio Inc. and/or its affiliates.
    
 This source code is the property of QuantumBio Inc. and/or its affiliates
 and is considered secret.

 This source code contains proprietary and Confidential Information, 
 including trade secrets, belonging to QuantumBio Inc. and/or its 
 affiliates.

 Use, duplication, or disclosure of this source code is strictly 
 prohibited without prior written authorization by the management of 
 QuantumBio Inc. At no time are you authorized to repurpose the source
 code for use with software other then that which it was intended.

 Please see http://www.quantumbioinc.com/ for more information.

 ***********************************************************************/
 // END COPYRIGHT
#endif      

    //    qmspanel.svl    User-interface entrypoint to QMScore calculations

#set title   'QuantumBio QMScore Panel'
#set class   'QuantumBio'
#set version '2010.02'
#set main    'qb_QMScoreSetupPanel'


function qb_CoreHandler;
function qb_GetCoreEnv;
function qb_GetCorePanel;
function qb_GetDefaultsCore;
function qb_GetDefaultsStatus;
function qb_GetEnvApp;
function qb_GetWhich;
function qb_InitCorePanel;
function qb_IsModeOneToOne;
function qb_IsShowingAllPoses;
function qb_ResetLigand;
function qb_LoadCoreScoreDB;
function qb_SaveDivConStatus;
function qb_SaveOptions;
function qb_SetAppStatus;
function qb_SetUpScoreDB;
function qb_TagCoreVals;
function qb_UpdateCorePanel;
function qb_VerifyDivConComplex;

function qb_QMScore;
function qb_QMScoreBatch;
function qb_QMScoreMDB;

function qb_GetCoreFields;
function qb_GetQMS_dbFields;

function pro_Join;

    //  Data tags are in the order in which they are returned by DivCon.

const QMS_DATA_TAGS =
    [
    "LIGAND",
    "TOTAL SCORE",
    "GAS PHASE HEAT OF FORMATION",
    "ELECTROSTATIC SOLVATION",
    "ATTRACTIVE LENNARD JONES",
    "SOLVATION ENTROPY",
    "VIBRATIONAL ENTROPY",
    "ELECTRONIC INTERACTION ENERGY",
    "MRM STERIC ENERGY",
    "MRM ELECTROSTATIC ENERGY",
    "MRM SOLVATION ENERGY",
    "MRM TOTAL ENERGY"
    ];
 
const QMS_NUM_FIELDS =
    [
    'QMScore',
    'dHf_g',
    'Solv_elec',
    'LJ_att',
    'S_solv',
    'S_vib',
    'E_eInt',
    'MRMScore',
    'E_stericMRM',
    'E_elecMRM',
    'E_solvMRM'
    ];

/*  NOTE:  If you change the Option lists here, you need to update the
 *         Keys in qmsinterface.svl.
 */

const QMSCORE_OPT_DEFAULTS =
        [
        ['freq',         0             ],
        ['opt',          1             ],
        ['shift',        9.0           ],
        ['hamrdio',      'PM3'         ],
        ['ham',          5             ],
        ['analpwd',      1             ]       //  Generate PWD row data
    ];

const LIG_OPTS =
    [
    'none',
    'free',
    'bound'
    ];

const ALG_OPTS =
    [
    'Linear Scaling',
    'Cubic Scaling'
    ];

const HAMILTONIANS =
    [
    'AM1',
    'MNDO',
    'MNDO/d',
    'PDDG-PM3',
    'PM3',
    'PM6'
    ];

const HEADER = 
    [
    name: 'panel', 
    title: 'QMScore',
    windowName: 'QMScorePanel',
    text: ['Run', 'Create', 'Update', 'Load', 'Close'],
    onTrigger: ['validate', 'validate', 'return', 'return', 'return'],
    bubbleHelp: ['Execute QMScore from MOE Window.',
        'Create a QMScore database with receptor(s), ligand(s)\n'
        'and run parameters for later execution on cluster.', 
        'Update only the run parameters in the QMScore database.', 
        'Load run parameters and complex from a QMScore database.', 
        '']
    ];


    const PANEL =
        [
        Vbox:
            [ extendH: 1,
                Mbox : 
                    [ 	    columns: 2, columnMajor:1, uniformCols:1,
                    name: 'calcbox', extendH: 1,
                    Option :
                        [
                        name: 'hamrdio', title: 'Hamiltonian:', text: HAMILTONIANS,
                        type: 'char', minWidth: 8,
                        bubbleHelp:
                            ''
                        ],
                    Option :
                        [
                        name: 'scaling', title: 'Calculation:', text: ALG_OPTS,
                        type: 'int', minWidth: 8,
                        bubbleHelp:
                            'Linear vs. Cubic refers to the scaling factor of the\n'
                            'calculation where Linear scales accordingly to O(n)\n'
                            'and Cubic scaling is the more conventional variety\n'
                            '(e.g. O(n^3) or worse) where n corresponds to the number\n'
                            'of electrons in the system. The cross over point is at\n'
                            'approximately 450 atoms.'
                        ],
                    Option :
                        [
                        name: 'opt', title: 'LigOpt:', text: LIG_OPTS,
                        type: 'int', minWidth: 8, extendH: 1,
                        bubbleHelp:
                            'A QM-based ligand optimization can be performed\n'
                            'prior to scoring. This optimization may occur\n'
                            'either while the ligand is docked within the target\n'
                            'or while the ligand is separate from the target.\n'
                            'Warning: this optional step will significantly\n'
                            'increase the CPU cost of the calculation.'
                        ],
                    Text :
                        [
                        name: 'shift', title: 'Shift:', minWidth: 8, extendH: 1, 
                        type: 'real', allowBlank: 0,
                        shortcut: ['0.0', '2.0', '4.0', '8.0', '16.0'],
                        bubbleHelp:
                            'Initial dynamic level shift (eV).'
                        ]
                    ],
            Separator : [flushLeft:1, extendH:1, margin:1],
                Hbox:   
                    [
                    name: 'analbox', extendH: 1,
                    Checkbox :
                        [
                        name: 'freq', text: 'Frequency',title: 'Generate:',
                        bubbleHelp:
                            'Perform frequency calculation to determine ligand\n'
                            'entropy in score function.  Warning: may increase\n'
                            'CPU cost of the calculation.'
                        ],
                    Checkbox :
                        [
                        name: 'analpwd', text: 'PWD', 
                        bubbleHelp:
                            'Generate pair-wise decomposition data\n'
                            'after the QMScore calculations.'
                        ] 
                    ]
           ],
                           Data :  [   name: 'ham'  ]
        ];

    //  Local Functions

/****************************************************************************
 *                          LoadDB
 *
 *  Get the core enviroment and application environment.
 *
 ****************************************************************************/
local function LoadDB [dbpath]

    local [dbkey, receptor, ligand, status, coreopts, c, mess] = 
        qb_LoadCoreScoreDB [dbpath, 'qmscore'];
    if dbkey === 0 and status === [] and coreopts === [] then
        Warning mess;
        exit [];
    endif

    local qmsopts = qb_GetEnvApp [dbkey];
    qmsopts = tagcat [qmsopts, tag tr QMSCORE_OPT_DEFAULTS];

    return [dbkey, receptor, ligand, status, coreopts, qmsopts];
endfunction

function qb_TagQMSVals;


local function UpdateDB[vals]
    local coreopts = qb_TagCoreVals [vals];
    local qmsopts = qb_TagQMSVals [vals];

    qb_SaveOptions [coreopts, qmsopts];

    local msg_key = Message [0, 'MOEDivCon QMScore update is done.'];
    sleep 1;
    Message [msg_key];
endfunction

function qb_GetQMSEnv;

    //  Global Functions

global function qb_GetQMS_dbFields []
    return QMS_NUM_FIELDS;
endfunction

/****************************************************************************
 *                          qb_GetQMSEnv
 *
 *  Get the core enviroment and application environment.
 *
 ****************************************************************************/
global function qb_GetQMSEnv [dbkey]

    if isnull dbkey then
        return [];
    endif
    local [receptor, ligand, status, coreopts, c] = qb_GetCoreEnv [dbkey];
    local qmsopts = qb_GetEnvApp [dbkey];
    qmsopts = tagcat [qmsopts, tag tr QMSCORE_OPT_DEFAULTS];

    return [receptor, ligand, status, coreopts, qmsopts];
endfunction

/****************************************************************************
 *                          qb_SaveQMScore
 *
 *  Save just the score data in the QMScore database.
 *
 ****************************************************************************/
global function qb_SaveQMScore [dbkey, rowkey, scoredata]

    if isnull dbkey then
        return [];
    endif
    if alltrue (QMS_DATA_TAGS === app first scoredata) then
        return db_Write [dbkey, rowkey, tag [QMS_NUM_FIELDS, 
            dropfirst app second scoredata]];
    endif

    return 0;
endfunction

/****************************************************************************
 *                          qb_CreateQMScoreDB
 *
 *  Create the QMScore DB as required and return the dbkey.
 *
 *  SVL API Purpose: to wrap the creation process with QMS_NUM_FIELDS et al so
 *      that other functions can create this DB (even if called from outside
 *      qmspanel.svl file).
 *
 * lmw TODO: need to call this function instead of qb_SetUpScoreDB 
 *      directly in qb_QMScoreSetupPanel;
 *
 ****************************************************************************/
global function qb_CreateQMScoreDB [status,coreopts,qmsopts]
    return qb_SetUpScoreDB [status, coreopts, qmsopts,
        [], QMS_NUM_FIELDS, 'qmscore', status.notforced];
endfunction

/****************************************************************************
 *                          qb_TagQMSVals
 *
 *  Tag values for QMScore options from the panel window.
 *
 *  SVI API Purpose: to wrap the creation process with QMSCORE_OPT_DEFAULTS et al so
 *      that other functions can process these options.
 *
 * lmw TODO: need to call this function instead of tag et al 
 *      directly in qb_QMScoreSetupPanel;
 *
 ****************************************************************************/
global function qb_TagQMSVals [vals]
    local qmsopts = tag [first tr QMSCORE_OPT_DEFAULTS, [
                        vals.freq, vals.opt, vals.shift, vals.hamrdio, vals.ham,
                        vals.analpwd]];
    return tagcat [qmsopts,tag tr QMSCORE_OPT_DEFAULTS];
endfunction

/****************************************************************************
 *
 *                          qb_QMScoreSetupPanel
 *
 ****************************************************************************/
global function qb_QMScoreSetupPanel []

    static status;                     //  Shared with child tasks.
    static origligmol;                 //  Probably not necessary for QMScore
    static ligatoms;

    if MOE_BATCH then exit ['Should only be run from MOE.']; endif
    local yesrun = not OS_MSWIN;
    if WindowShow ['QMScorePanel', 1] then return; endif

    local coreopts = [], qmsopts = [], browsetasks = [], dbkey = 0;
    static dbFileName = [];
    status = qb_GetDefaultsStatus [];
    status = qb_SetAppStatus [status, 'qms'];
    local coredflts = qb_GetDefaultsCore [];

    local appdflts = tag tr QMSCORE_OPT_DEFAULTS;
    coredflts = tagpoke [coredflts, 'scoredb', 'qmscore.mdb'];

    local wkey = WindowCreate cat [HEADER, qb_GetCorePanel [], PANEL];
    status = qb_InitCorePanel [wkey, status, coredflts];
    WindowSetData [wkey, appdflts];
    WindowSetAttr [wkey, [panel:    [sensitive: [yesrun, 1, 1, 1, 1]]]];
    WindowSetAttr [wkey, [freq:    [sensitive:0]]];
    WindowSetAttr [wkey, [hamrdio:    [text: HAMILTONIANS | [1,0,0,0,1,0]]]];
    WindowShow [wkey, 1];

    loop
        local [vals, trig] = WindowWait wkey;
        vals.ham = indexof [vals.hamrdio,HAMILTONIANS];
        if trig === 'panel' then
            if vals.panel === 'Close' then 
                break;

            elseif vals.panel === 'Load' then 

                if second task_fork [master: 'parent',
                        statics:'share'] === 'child' then
                    task_prio 0;

                    local dbpath = first FilePrompt [title: 'Load Database', 
                        filter: '*.mdb', allowEmptyList: 0];
                    if not isnull dbpath then
                        if neL [ftype dbpath, 'file']
                                or neL [fext dbpath, 'mdb'] then
                            Warning 'The QMScore database doesn\'t exist.';
                            exit [];
                        endif

                        local ldrecs, ldligs, ldsts,ldcoreopts,ldqmsopts;
                        [dbkey, ldrecs, ldligs, ldsts, ldcoreopts,
                            ldqmsopts] = LoadDB [dbpath];
                        Close [force:1];
                        local idx = 1;
                        if length ldrecs >= 1 then
                            local ldrec = ldrecs(idx);
                            local ldlig = ldligs(idx);
                            if neL [ldrec, []] and neL [ldlig, []] then
                                origligmol = ldlig;
                                local chainsList = mol_Create ldrec;
                                oSetCollection ['Receptor', cat oAtoms chainsList];
                                ligatoms = cat oAtoms mol_Create origligmol;
                                oSetCollection ['QB_LIGAND', ligatoms];
                                oSetCollection ['LIGX_LIGAND', ligatoms];
                                aSetNucleusLook [ligatoms, 'small-sphere'];
                                aSetBondLook [ligatoms, 'cylinder'];
                            endif
                        endif
                        View [];
                        dbFileName = db_Filename dbkey;
                        dbv_Open dbFileName;
                        db_Close dbkey;
                        ldcoreopts.scoredb=dbpath;
                        status = qb_UpdateCorePanel [wkey, ldsts, ldcoreopts];
                        WindowSetAttr [wkey, [modebox:  [sensitive: 0]]]; // when updating, can't change mode.
                        WindowSetAttr [wkey, [recbox:   [sensitive: 0]]]; // when updating, can't change mode.
                        WindowSetAttr [wkey, [posebox:  [sensitive: 0]]]; // when updating, can't change mode.
                        WindowSetAttr [wkey, [dbbox:    [sensitive: 0]]]; // when updating, can't change mode.
                        WindowSetAttr [wkey, [panel:    [onTrigger: ['return', 'validate', 'return', 'return', 'return']]]];
                        WindowSetAttr [wkey, [panel:    [sensitive: [yesrun, 0, 1, 1, 1]]]];
                        WindowSetData [wkey, ldqmsopts];
                    endif

                    exit [];
                endif

            elseif vals.panel === 'Run' then

                if qb_IsShowingAllPoses [vals] then
                    [vals, ligatoms] = qb_ResetLigand [wkey, vals];
                endif

                if second task_fork [statics:'share'] === 'child' then
                    task_prio 3;
                    task_settitle [-1, 'MOEDivCon QMScore Run'];

                    status = qb_VerifyDivConComplex [0, status, vals];
                    if not status.verified then exit []; endif

                    coreopts = qb_TagCoreVals [vals];
                    qmsopts = qb_TagQMSVals[vals];

                    if dbFileName === [] then
                        dbkey = qb_SetUpScoreDB [status, coreopts, qmsopts, 
                            [], QMS_NUM_FIELDS, 'qmscore', status.notforced];
                        dbFileName = db_Filename dbkey;
                        db_Close dbkey;
                    else
                        UpdateDB [vals];
                    endif
                    dbv_Open dbFileName;
                    qb_QMScoreMDB[dbFileName];

                    exit [];
                else
                    break;
                endif

            elseif vals.panel === 'Create' then

                if qb_IsShowingAllPoses [vals] then
                    [vals, ligatoms] = qb_ResetLigand [wkey, vals];
                endif

                if second task_fork [statics:'share'] === 'child' then
                    task_prio 0;

                    status = qb_VerifyDivConComplex [wkey, status, vals];
                    if not status.verified then exit []; endif

                    coreopts = qb_TagCoreVals [vals];
                    qmsopts = qb_TagQMSVals [vals];

                    dbkey = qb_SetUpScoreDB [status, coreopts, qmsopts,
                        [], QMS_NUM_FIELDS, 'qmscore', status.notforced];
                    dbv_Open db_Filename dbkey;
                    db_Close dbkey;
                    exit [];
                endif

            elseif vals.panel === 'Update' then

                if second task_fork [statics:'share'] === 'child' then
                    task_prio 0;

                    UpdateDB [vals];
                    sleep 2;
                    exit [];
                endif
            endif

        else
           [status, ligatoms, browsetasks] = qb_CoreHandler [wkey, trig, 
                status, vals, browsetasks];
        endif
    endloop

    task_kill browsetasks;
    WindowDestroy wkey;

endfunction
